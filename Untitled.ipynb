{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "d426a910",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Preparing Q# environment...\n"
     ]
    }
   ],
   "source": [
    "import qsharp"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "9a332812",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "D:\\snippet_.qs(93,27): warning QS3308: Deprecated syntax. Use [] to construct an empty array, or [x, size = n] to construct an array of x repeated n times.\r\n",
      "D:\\snippet_.qs(153,26): warning QS3308: Deprecated syntax. Use [] to construct an empty array, or [x, size = n] to construct an array of x repeated n times.\r\n"
     ]
    }
   ],
   "source": [
    "%%qsharp\n",
    "\n",
    "\n",
    "    open Microsoft.Quantum.Intrinsic;\n",
    "    open Microsoft.Quantum.Canon;\n",
    "    open Microsoft.Quantum.Measurement;\n",
    "    open Microsoft.Quantum.Convert;\n",
    "    open Microsoft.Quantum.Arrays;\n",
    "    open Microsoft.Quantum.Math;\n",
    "    open Microsoft.Quantum.Diagnostics;\n",
    "\n",
    "    /// # Summary\n",
    "    /// This operation applies the X-rotation to each qubit. We can think of it as time \n",
    "    /// evolution induced by applying a Hamiltonian that sums over all X rotations.\n",
    "    ///\n",
    "    /// # Description\n",
    "    /// The driver Hamiltonian is defined as:\n",
    "    ///    H = - \\sum_i X_i for time t.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## time\n",
    "    /// Time passed in evolution of X rotation\n",
    "    /// ## target\n",
    "    /// Target qubit register\n",
    "    operation ApplyDriverHamiltonian(time: Double, target: Qubit[]) : Unit is Adj + Ctl {\n",
    "        ApplyToEachCA(Rx(-2.0 * time, _), target);\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// This applies the Z-rotation according to the instance Hamiltonian. \n",
    "    /// We can think of it as Hamiltonian time evolution for time t induced\n",
    "    /// by an Ising Hamiltonian. The Ising Hamiltonian sums over all connected\n",
    "    /// pairs of Pauli-Z operations Z_i and Z_j scaled by a factor J_ij, plus \n",
    "    /// the sum over all Z_i scaled by a factor h_i.\n",
    "    ///\n",
    "    /// # Description\n",
    "    /// The Ising Hamiltonian is defined as:\n",
    "    ///     $\\sum_ij J_ij Z_i Z_j + \\sum_i h_i Z_i$.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## time\n",
    "    /// Time point in evolution.\n",
    "    /// ## weights\n",
    "    /// Ising magnetic field or \"weights\" encoding the constraints of our\n",
    "    /// traveling Santa problem.\n",
    "    /// ## coupling\n",
    "    /// Ising coupling term or \"penalty\" encoding the constraints of our\n",
    "    /// traveling Santa problem.\n",
    "    /// ## target\n",
    "    /// Qubit register that encodes the Spin values in the Ising Hamiltonian.\n",
    "    operation ApplyInstanceHamiltonian(\n",
    "        numSegments : Int,\n",
    "        time : Double, \n",
    "        weights : Double[], \n",
    "        coupling : Double[],\n",
    "        target : Qubit[]\n",
    "    ) : Unit {\n",
    "        use auxiliary = Qubit();\n",
    "        for (h, qubit) in Zipped(weights, target) {\n",
    "            Rz(2.0 * time * h, qubit);\n",
    "        }\n",
    "        for i in 0..5 {\n",
    "            for j in i + 1..5 {\n",
    "                within {\n",
    "                    CNOT(target[i], auxiliary);\n",
    "                    CNOT(target[j], auxiliary);\n",
    "                } apply {\n",
    "                    Rz(2.0 * time * coupling[numSegments * i + j], auxiliary);\n",
    "                }\n",
    "            }\n",
    "        }\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// Calculate Hamiltonian parameters based on the given costs and penalty.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## segmentCosts\n",
    "    /// Cost values of each segment.\n",
    "    /// ## penalty\n",
    "    /// Penalty for cases that don't meet constraints.\n",
    "    ///\n",
    "    /// # Output\n",
    "    /// ## weights\n",
    "    /// Hamiltonian parameters or \"weights\" as an array where each element corresponds \n",
    "    /// to a parameter h_j for qubit state j.\n",
    "    /// ## numSegments\n",
    "    /// Number of segments in the graph that describes possible paths.\n",
    "    function HamiltonianWeights(\n",
    "        segmentCosts : Double[], \n",
    "        penalty : Double, \n",
    "        numSegments : Int\n",
    "    ) : Double[] {\n",
    "        mutable weights = new Double[numSegments];\n",
    "        for i in 0..numSegments - 1 {\n",
    "            set weights w/= i <- 4.0 * penalty - 0.5 * segmentCosts[i];\n",
    "        }\n",
    "        return weights;\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// Calculate Hamiltonian coupling parameters based on the given penalty.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## penalty\n",
    "    /// Penalty for cases that don't meet constraints.\n",
    "    /// ## numSegments\n",
    "    /// Number of segments in the graph that describes possible paths.\n",
    "    ///\n",
    "    /// # Output\n",
    "    /// ## coupling\n",
    "    /// Hamiltonian coupling parameters as an array, where each element corresponds\n",
    "    /// to a parameter J_ij between qubit states i and j.\n",
    "    function HamiltonianCouplings(penalty : Double, numSegments : Int) : Double[] {\n",
    "        // Calculate Hamiltonian coupling parameters based on the given costs and penalty\n",
    "        // Most elements of J_ij equal 2*penalty, so set all elements to this value, \n",
    "        // then overwrite the exceptions. This is currently implemented for the\n",
    "        // example with 6 segments.\n",
    "        EqualityFactI(numSegments, 6, \n",
    "            \"Currently, HamiltonianCouplings only supports given constraints for 6 segments.\"\n",
    "        );\n",
    "        return ConstantArray(numSegments * numSegments, 2.0 * penalty)\n",
    "            w/ 2 <- penalty\n",
    "            w/ 9 <- penalty\n",
    "            w/ 29 <- penalty;\n",
    "    }\n",
    "    \n",
    "    /// # Summary\n",
    "    /// Perform the QAOA algorithm for this Ising Hamiltonian\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## numSegments\n",
    "    /// Number of segments in graph\n",
    "    /// ## weights\n",
    "    /// Instance Hamiltonian parameters or \"weights\" as an array where each \n",
    "    /// element corresponds to a parameter h_j for qubit state j.\n",
    "    /// ## couplings\n",
    "    /// Instance Hamiltonian coupling parameters as an array, where each \n",
    "    /// element corresponds to a parameter J_ij between qubit states i and j.\n",
    "    /// ## timeX\n",
    "    /// Time evolution for PauliX operations\n",
    "    /// ## timeZ\n",
    "    /// Time evolution for PauliX operations\n",
    "    operation PerformQAOA(\n",
    "            numSegments : Int, \n",
    "            weights : Double[], \n",
    "            couplings : Double[], \n",
    "            timeX : Double[], \n",
    "            timeZ : Double[]\n",
    "    ) : Bool[] {\n",
    "        EqualityFactI(Length(timeX), Length(timeZ), \"timeZ and timeX are not the same length\");\n",
    "\n",
    "        // Run the QAOA circuit\n",
    "        mutable result = new Bool[numSegments];\n",
    "        use x = Qubit[numSegments];\n",
    "        ApplyToEach(H, x); // prepare the uniform distribution\n",
    "        for (tz, tx) in Zipped(timeZ, timeX) {\n",
    "            ApplyInstanceHamiltonian(numSegments, tz, weights, couplings, x); // do Exp(-i H_C tz)\n",
    "            ApplyDriverHamiltonian(tx, x); // do Exp(-i H_0 tx)\n",
    "        }\n",
    "        return ResultArrayAsBoolArray(MultiM(x)); // measure in the computational basis\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// Calculate the total cost for the given result.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## segmentCosts\n",
    "    /// Array of costs per segment\n",
    "    /// ## usedSegments\n",
    "    /// Array of which segments are used\n",
    "    ///\n",
    "    /// # Output\n",
    "    /// ## finalCost\n",
    "    /// Calculated cost of given path\n",
    "    function CalculatedCost(segmentCosts : Double[], usedSegments : Bool[]) : Double {\n",
    "        mutable finalCost = 0.0;\n",
    "        for (cost, segment) in Zipped(segmentCosts, usedSegments) {\n",
    "            set finalCost += segment ? cost | 0.0;\n",
    "        }\n",
    "        return finalCost;\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// Final check to determine if the used segments satisfy our known \n",
    "    /// constraints. This function is implemented to consider a graph with 6 \n",
    "    /// segments and three valid connected paths.\n",
    "    ///\n",
    "    /// # Input\n",
    "    /// ## numSegments\n",
    "    /// Number of segments in the graph\n",
    "    /// ## usedSegments\n",
    "    /// Array of which segments were used\n",
    "    ///\n",
    "    /// # Output\n",
    "    /// ## output\n",
    "    /// Boolean value whether the conditions are satisfied.\n",
    "    function IsSatisfactory(numSegments: Int, usedSegments : Bool[]) : Bool {\n",
    "        EqualityFactI(numSegments, 6, \n",
    "            \"Currently, IsSatisfactory only supports constraints for 6 segments.\"\n",
    "        );\n",
    "        mutable hammingWeight = 0;\n",
    "        for segment in usedSegments {\n",
    "            set hammingWeight += segment ? 1 | 0;\n",
    "        }\n",
    "        if (hammingWeight != 4 \n",
    "            or usedSegments[0] != usedSegments[2] \n",
    "            or usedSegments[1] != usedSegments[3] \n",
    "            or usedSegments[4] != usedSegments[5]) {\n",
    "            return false;\n",
    "        }\n",
    "        return true;\n",
    "    }\n",
    "\n",
    "    /// # Summary\n",
    "    /// Run QAOA for a given number of trials on 6 qubits. This sample is based \n",
    "    /// on the Traveling Santa Problem outlined here: \n",
    "    ///     http://quantumalgorithmzoo.org/traveling_santa/.\n",
    "    /// Reports on the best itinerary for the Traveling Santa Problem and how \n",
    "    /// many of the runs resulted in the answer. This should typically return \n",
    "    /// the optimal solution roughly 71% of the time.\n",
    "    /// \n",
    "    /// # Input\n",
    "    /// ## numTrials\n",
    "    /// Number of trials to run the QAOA algorithm for.\n",
    "    @EntryPoint()\n",
    "    operation RunQAOATrials(numTrials : Int) : Unit {\n",
    "        let penalty = 20.0;\n",
    "        let segmentCosts = [4.70, 9.09, 9.03, 5.70, 8.02, 1.71];\n",
    "        let timeX = [0.619193, 0.742566, 0.060035, -1.568955, 0.045490];\n",
    "        let timeZ = [3.182203, -1.139045, 0.221082, 0.537753, -0.417222];\n",
    "        let limit = 1E-6;\n",
    "        let numSegments = 6;\n",
    "\n",
    "        mutable bestCost = 100.0 * penalty;\n",
    "        mutable bestItinerary = [false, false, false, false, false];\n",
    "        mutable successNumber = 0;\n",
    "\n",
    "        let weights = HamiltonianWeights(segmentCosts, penalty, numSegments);\n",
    "        let couplings = HamiltonianCouplings(penalty, numSegments);\n",
    "\n",
    "        for trial in 0..numTrials {\n",
    "            let result = PerformQAOA(\n",
    "                numSegments, \n",
    "                weights, \n",
    "                couplings, \n",
    "                timeX, \n",
    "                timeZ\n",
    "            );\n",
    "            let cost = CalculatedCost(segmentCosts, result);\n",
    "            let sat = IsSatisfactory(numSegments, result);\n",
    "            Message($\"result = {result}, cost = {cost}, satisfactory = {sat}\");\n",
    "            if (sat) {\n",
    "                if (cost < bestCost - limit) {\n",
    "                    // New best cost found - update\n",
    "                    set bestCost = cost;\n",
    "                    set bestItinerary = result;\n",
    "                    set successNumber = 1;\n",
    "                } elif (AbsD(cost - bestCost) < limit) {\n",
    "                    set successNumber += 1;\n",
    "                }\n",
    "            }\n",
    "        }\n",
    "        let runPercentage = IntAsDouble(successNumber) * 100.0 / IntAsDouble(numTrials);\n",
    "        Message(\"Simulation is complete\\n\");\n",
    "        Message($\"Best itinerary found: {bestItinerary}, cost = {bestCost}\");\n",
    "        Message($\"{runPercentage}% of runs found the best itinerary\\n\");\n",
    "    }\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "3367776e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,True,True,True], cost = 29.16, satisfactory = False\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,False,False,True,True], cost = 14.43, satisfactory = False\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [False,False,False,False,True,True], cost = 9.73, satisfactory = False\r\n",
      "result = [True,True,True,True,False,False], cost = 28.52, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,False,True], cost = 15.440000000000001, satisfactory = False\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "result = [True,False,True,False,True,True], cost = 23.46, satisfactory = True\r\n",
      "Simulation is complete\n",
      "\r\n",
      "Best itinerary found: [True,False,True,False,True,True], cost = 23.46\r\n",
      "80% of runs found the best itinerary\n",
      "\r\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "()"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "RunQAOATrials.simulate(numTrials=20)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bad307fc",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.13"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
